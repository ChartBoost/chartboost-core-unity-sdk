plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdkVersion = 33
    buildToolsVersion = '33.0.0'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
    }
    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

repositories {
    google()
    mavenCentral()
    maven {
        url "https://cboost.jfrog.io/artifactory/chartboost-core/"
    }
    maven {
        url "https://cboost.jfrog.io/artifactory/private-chartboost-core/"
        credentials {
            username System.getenv("JFROG_USER")
            password System.getenv("JFROG_PASS")
        }
    }
}

dependencies {
    //Make sure to bump the version on new releases.
    api fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.chartboost:chartboost-core-sdk:0.4.0'
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("androidx.core:core-ktx:$kotlin_version")
}

task assembleForPlugin(type: Jar, dependsOn: 'bundleLibRuntimeToJarRelease') {
    dependsOn 'androidDependencies'
    // We are only interested in the classes only.
    from zipTree("$projectDir/build/intermediates/runtime_library_classes_jar/release/classes.jar")
    include '**/*.class'
    exclude 'META-INF/'
    exclude 'META-INF/MANIFEST.MF'
}

task copyToDesiredLocation(type: Copy, dependsOn: 'assembleForPlugin') {
    from ("$projectDir/build/intermediates/runtime_library_classes_jar/release/classes.jar")
    into ("$rootDir/../Runtime/Android/Plugins/Android")
    rename("classes.jar", "chartboost-core-android-bridge.jar")
}
